---
title: "Nonlinear DID:"
subtitle: "Lessons from Staggered Linear Differences in Differences"
author: 
  - name: Fernando Rios-Avila
    affiliation: Levy Economics Institute
format: 
    revealjs:
        slide-number: true
        width: 1600
        height: 900
        code-fold: false
        css: styles.css 
    pdf: default    
bibliography: did_reg.bib        
editor: visual    
jupyter: nbstata
---

## Introduction

- **Differences in Differences** (DiD) design is one of the most popular methods in applied microeconomics, because it requires relatively few assumptions to identify treatment effects.
  - No anticipation,
  - Parallel trends,
  - No spillovers
- The canonical DiD, a 2x2 design, simply compares means (or conditional means) of the outcome variable (before after x treated non-treated) to identify treatment effects. 
  - Thus it can be used even if outcome is a limited dependent variable (binary, count, etc) (parallel to the linear regression case)

## 

- Because the Canonical design is rather limited, many extensions have been proposed to handle more complex scenarios: Staggered Treatment with **GxT DID**.
  - Early Extensions (infamous TWFE) have been shown to be problematic. (negative weights and bad controls)
  - but more recent approaches (See @roth_whats_2023) have shown how can one better use these designs to identify treatment effects, avoiding the simple-TWFE problems.

- Linear models, however, have limitations:
  - Linear models do a poor job interpolating and predicting LDV outcomes
  - Parallel trends assumptions may only be credible under specific functional forms

# Disclaimer:
who am I not?

## Not Andrew Goodman-Bacon 

:::: {.columns}

::: {.column width="40%"}
![](images/paste-5.png){width="400"}

:::

::: {.column width="60%"}

- Among others, Andrew showed the problems of using TWFE in the presence of staggered adoption of the treatment.

- Because of treatment timing, later treated units are compared to **bad** controls (early treated ones), in potential violation of the parallel trends assumption.

- This also relates to negative weights.

- See @goodmanbacon2021

:::

::::

## Not Pedro Sant'Anna 

:::: {.columns}

::: {.column width="40%"}
![](images/paste-4.png){width="400"}
:::

::: {.column width="60%"}

- Pedro and Brantly proposed deconstructing the GxT problem. Consider only good 2x2 DD designs to identify Treatment effects in DID.
- Agregate them as needed to obtain the desired treatment effect (weighted Average). (dynamic, average, across time, across groups, etc)
  
- Along with **Jonathan Roth**, discuss the problem of PTA and functional forms. Not all may be valid.

- see @callaway_2021 and @rothsantanna2023
  
:::

::::

## Not Jeffrey Wooldridge 

:::: {.columns}

::: {.column width="40%"}
![](images/paste-1.png){width="400"}

:::

::: {.column width="60%"}

- Jeff Wooldridge brought back life to the **TWFE**. 
- The problem was not the **TWFE** part of the analysis, but the model specification. 
  - ($post \times treated$ instead of $G \times T$)
- This insights, can be extended to nonlinear cases.
- See @wooldridge_2021 and @wooldridge_2023
  
:::

::::

## Fernando Rios-Avila  

:::: {.columns}

::: {.column width="40%"}
![](images/paste-7.png){width="400"}

:::

::: {.column width="60%"}

- I have followed some of the developments in DID with staggered adoption of the treatment. 
  - Implemented few things (`drdid`/`csdid`/`csdid2`/`jwdid`)
  - Understood few others (comparison groups, efficiency, negative weights, nonlinear models)
- And today, I will be providing some of my insights regarding the empirical analysis of nonlinear DID.
  - I will rely heavily on [@wooldridge_2023](https://academic.oup.com/ectj/article/26/3/C31/7250479), 
  

:::

::::

## Basics: 2x2 DiD

- In the 2x2 DID design, we have 2 groups:
  - Control ($D=0$) and treated ($D=1$), 
- Which are observed for two periods of time:
  - Before ($T=0$) and after ($T=1$) the treatment.

- For all groups and periods of time, we observe the *realized* outcome $Y_{i,t}$, but cannot observe all *potential* outcomes $Y_{i,t}(D)$.

- Realized outcomes are determined by the following equation:

$$Y_{i,t}=D_i Y_{i,t}(1) + (1-D_i) Y_{i,t}(0)
$$

## 

If treatment occured at some point between T0 and T1, and we could observe all potential outcomes, the estimate of interested, Average Treatment effect, could be estimated as follows:

$$ATT = E(Y_{i,1}(1) - Y_{i,1}(0)|D_i=1)
$$

For the treated, we observe $Y_{i,1}(1)$, but cannot observe $Y_{i,t}(0)$ (counterfactual), thus, to identify Treatment Effects, we need to impose some assumptions.

- PTA: 
  
$$\begin{aligned}
E(Y_{i,1}(0) - Y_{i,0}(0)|D_i=1) &= E(Y_{i,1} - Y_{i,0}|D_i=0) \\
E(Y_{i,1}(0)|D_i=1) &= E(Y_{i,0}(0)|D_i=1) + E(Y_{i,1} - Y_{i,0}|D_i=0) 
\end{aligned}
$$

- No Anticipation: 
$$Y_{i,0}(1) =  Y_{i,0}(0)=Y_{i,0}$$

## 

Thus, ATT can be estimated as follows:

$$\begin{aligned}
ATT &= E(Y_{i,1}(1)|D_i=1)-E(Y_{i,1}(0)|D_i=1) \\
&= E(Y_{i,1}|D_i=1)- \Big(E(Y_{i,0}|D_i=1) + E(Y_{i,1} - Y_{i,0}|D_i=0)  \Big) \\
&= E(Y_{i,1}-Y_{i,0}|D_i=1)- E(Y_{i,1} - Y_{i,0}|D_i=0)  
\end{aligned}
$$

- And the Same could be done via Regressions:

$$y_{i,t} = \beta_0 + \beta_1 T + \beta_2 D_i + \theta (D_i \times T) + \epsilon_{i,t}
$$

- ATT identification relies on the Parallel trend assumption. 

## How to test for PTA?

- PTA is a non-testable assumption, because we do not observe all potential outcomes.
- However, if we "move", from the 2x2 design, it may be possible to test if PTA hold Before treatment.

- Consider a case of T periods of time, and that treatment happen at period G.

- Say we estimate the ATT comparing periods T and T-1, for any T<G.

$$ATT(T) = E(Y_{i,T} - Y_{i,T-1}|D_i=1) - E(Y_{i,T} - Y_{i,T-1}|D_i=0)
$$

- If there is no anticipation, and Parallel trends hold, then $ATT(T)=0 \text{ if } T<G$ 
  - This is what @callaway_2021 uses for PTA testing

## How to test for PTA?

- Alternatively, one could simply estimate all ATT's using period G-1 as baseline period (`long2` differences):

$$ATT^2(T) = E(Y_{i,T} - Y_{i,G-1}|D_i=1) - E(Y_{i,T} - Y_{i,G-1}|D_i=0)
$$
 
- And use all post-treatment periods to estimate the ATT ($T\geq G$)
- and use all pre-treatment periods to test for PTA ($T<G$)
  
## 

::: {.panel-tabset}

## What if PTA does not hold?

- As suggested by @wooldridge_2023, one of the reasons PTA may not hold is because we may be analyzing the wrong model.

  - consider two groups of workers, high and low earners, that experience the same wage growth. (parallel trends in relative terms)
  - If we observe wages at levels, parallel trends would be violated
  - And Post treatment estimates will be missleading
   
## Wage PTA in levels

```{stata}
*| output: false
*| echo: false
*| fig-align: center
 
set scheme white2
color_style tableau
clear
set obs 20
gen y1 = 10
gen y2 = 30
replace y1 = y1[_n-1]*(1.02 + (_n>11)*0.03) if _n>1
replace y2 = y2[_n-1]*1.02 if _n>1
scalar yy1 = y1[11]
scalar yy2 = y2[11]
replace y1=y1-yy1
replace y2=y2-yy2

gen t =_n
gen dy = y1 - y2
scatter y1 y2 dy t, connect(l l l) ///
legend(order(1 "Low earner" 2 "High earner" 3 "DID"))

graph export fig1.png, replace width(1000)
```

![](fig1.png){width="1000"}

## Wage PTA in logs

```{stata}
*| output: false
*| echo: false
*| fig-align: center

gen lny2=ln(y2+yy2) - ln(yy2) 
gen lny1=ln(y1+yy1) - ln(yy1)
gen dly = lny1 - lny2
replace  lny2=lny2-0.01
scatter lny1 lny2 dly t, connect(l l l) ///
legend(order(1 "Low earner" 2 "High earner" 3 "DID")) ///
xsize(12) ysize(8)
graph export fig2.png, replace width(1000)
```

![](fig2.png){width="1000"}


:::

## PTA may hold for $G(\bar Y)$

A similar story could be told about other types of transformations.

In general, it is possible that PTA hold for some other monotonic transformation of the outcome variable.

$$G^{-1}\Big( E_1 [Y_{i,1}(0)] \Big) -G^{-1}\Big(E_1[ Y_{i,0}(0) ] \Big)  
= G^{-1}\Big(E_0[Y_{i,1}]\Big) - G^{-1}\Big(E_0[Y_{i,0}]\Big)
$$

This is very similar to the PTA assumption explored in Roth and Sant'Anna (2023).

$$E_1\Big[g(Y_{i,1}(0))\Big] - E_1\Big[g(Y_{i,0}(0))\Big] = E_0\Big[g(Y_{i,1})\Big] - E_0\Big[g(Y_{i,0})\Big]
$$

Wooldridge idea: It may be possible to identify ATTs using correct functional forms, through the **latent index**.

## How do things Change?

- Using this insight, we can go back to the 2x2 design, and see how things change.

Before: 

$$E(y_{i,t}|D,T)=\beta_0 + \beta_1 T + \beta_2 D_i + \theta (D_i \times T)
$$

After:

$$E(y_{i,t}|D,T)=G\Big(\beta_0 + \beta_1 T + \beta_2 D_i + \theta (D_i \times T)\Big)
$$

- For the practitioner, the extended Nonlinear DID simply requires choosing a functional form that better fits the data.
  - Poisson, logit, ~~fractional regression~~, multinomial model, Linear model, etc
  

## Extension I: Adding Covariates

- Many papers in the literature consider the use of covariates in the estimation of the ATT.
  
- The lessons from @santanna_doubly_2020:
  - The choosen covariates should be time fixed, to avoid contamination of the treatment effect.
  - Using covariates allows relaxing the parallel trends assumption: PTA hold for specific groups of individuals. (if not for the whole population due to compositional changes)
  
- In the 2x2 DID-Regression, covariates can be added with interactions:

$$\begin{aligned}
y_{i,t} &= \beta_0 + \beta_1 D + \beta_2 T + \theta (D \times T) \\
&+ X\gamma +  D \times X\gamma_d + T \times X\gamma_T + D \times T (X-\bar X )\lambda + \epsilon_{i,t}
\end{aligned}
$$

- The same could be done in the nonlinear case

## Extension II: GxT DiD

- The 2x2 design is rather limited, because often people have access to multiple periods of time, with differential treatment timing. (staggered adoption of the treatment)
  - I call this the GxT design (G groups, T periods of time) 
- The majority of the papers that analyze this case impose an additional assumption: 
  - Treatment is not Reversable: Once treated always treated

**NOTE**: Because of the interactions required, adding covariates would rapidily "consume" degrees of freedom. (may be a problem with nonlinear models).

How to see this? `--> tab tvar gvar`

##
### The Problem

- Early extensions of the 2x2 design to the GxT design, relied on the **TWFE** estimator. 
$$y_{i,t} = \delta_i + \delta_t + \theta^{fe} D_{i,t} + e_{i,t}$$

where $D_{i,t}=1$ only after treatment is in place, and zero otherwise.

- This model has been shown to be problematic, because of How OLS estimates the $\theta^{fe}$ parameter.
  - $\theta^{fe}$ is a weighted average of all possible 2x2 DID designs. @goodmanbacon2021
  - Some designs use early treated units as controls for late treated units, which might be a violation of the parallel trends assumption. 
    - (treated units effectively receiving negative weights) @goodmanbacon2021, @dechaisemartin2020 and @borusyak2023revisiting. 

##
### Avoiding the Problem

- @callaway_2021 proposes a simple solution: Deconstruct the problem into smaller pieces (2x2 DIDs), and aggregate them as needed.

- @wooldridge_2021, however, proposes a different solution: Use the correct functional form to estimate the ATTs.

$$\text{Instead of: }Y_{i,t} = \delta_i + \gamma_t  + \theta^{fe} PT_{i,t} + \epsilon_{i,t}$$

$$\text{Use: }Y_{i,t} = \delta_i + \gamma_t  + \sum_{g \in \mathbb{G}} \sum_{t=g}^T \theta_{g,t} \mathbb{1}(G=g,T=t) + \epsilon_{i,t}$$

- Their Message: Embrace heterogeneity across time and cohorts.

## An added Insight

- The approach proposed by @wooldridge_2021, is more efficient than @callaway_2021, because it uses all pre-treatment data to estimate the ATTs. (@callaway_2021 uses only one pre-treatment period)

- However, doing this doesn't allow you to test for PTA directly, unless we use an alternative approach:

$$Y_{i,t} = \delta_i + \gamma_t  + 
\sum_{g \in \mathbb{G}} \sum_{t=t_0}^{g-2} \theta_{g,t}^{pre} \mathbb{1}(G=g,T=t) +
\sum_{g \in \mathbb{G}} \sum_{t=g}^T \theta_{g,t}^{post} \mathbb{1}(G=g,T=t) + \epsilon_{i,t}$$

- This specification is equivalent to @callaway_2021 and to @sun_estimating_2021. 
  - Its explicily a regression (Wooldridge)
  - and uses actual, instead of relative, time.
 
## Implementing NL-DID the `JW` way

- One of the advantages of the approaches proposed by @wooldridge_2021 and @wooldridge_2023, is that they can be directly estimated using regressions.
- The hard part is to construct all the interactions required for the model to work.
- And a second challenge is to aggregate the results.

**`jwdid`**

- `jwdid` is a simple command that helps with the construction of all required interactions that could be used to implement Wooldridge approach.
- It is flexible enough, in that it allows you to choose different estimators that would better fit your data.
- it comes with its own post estimation commands that can help you aggregate the results into simple ATT, dynamics effects, across periods, across years, etc.

- Lets take it for a spin

## Command Syntax

- `jwdid` - Command Name. In charge of getting all interactions -right-
  - `depvar` `[indepvar]` - Declare the dependent variables. Independent variables are optional. They should be time fixed.
  - `[if]` `[in]` `[weight]`, Declares sample and weights. Only `PW` is allowed. 

## Command Main Options

- `jwdid`: main options
  - `ivar(varname)`: Panel indicator. If not declared, command assumes one is using repeated cross sections.
  - `cluster(varname)`: Cluster variable. To request a clustered standard error other than at `ivar` level. Recommended with RC.
  - `tvar(varname)` or `time(varname)`: Required, Time variable. There are two ways to call it for compatability with `csdid`.
  - `gvar(varname)`: Group variable. Indicates the timing of when a unit has ben treated.
  - `trtvar(varname)`: If using Panel data, one could instead provide the post-treatment dummy.
  	- If data is repeated crossection, one requires using `trgvar(varname)` (Pseudo panel indicator).

## Extra Options

- `group`: Requests using group fixed effects, instead of individual fixed effects (default)
- `never`: Request to use alternative specification that allows to test for PTA. (default is to use the standard specification)
- Linear and Nonlinear models:
  - `method(command, options)`: Request to use a specific method to model the data. Default is using linear regression via `reghdfe`. 
  - the option part allows you do include specific options for the method. (e.g. `method(glm, link() family())`)

## Example 1: Min Wages on Employment CS data {.scrollable}

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
clear all
qui:ssc install frause
qui:frause mpdta, clear
frause mpdta, clear
jwdid lemp, ivar(county) tvar(year) gvar(first)
```

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
gen emp = exp(lemp)
jwdid emp, ivar(county) tvar(year) gvar(first) method(poisson) 
```

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
jwdid emp, ivar(county) tvar(year) gvar(first) method(poisson) never
```

## Example 1: Aggregations {.scrollable}

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
estat event
```

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
estat group
```

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
estat calendar
```

```{stata}
*| echo: true
*| output: false
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
qui:estat event, plot
graph export event.png, replace width(1000)
```

![](event.png)

## Example 2: Wooldridge Simulation data {.scrollable}

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
clear all
use nonlinear_did/did_common_6_binary, clear
qui {
reg y i.w#c.d#c.f04 i.w#c.d#c.f05 i.w#c.d#c.f06 ///
	i.w#c.d#c.f04#c.x i.w#c.d#c.f05#c.x i.w#c.d#c.f06#c.x ///
	f02 f03 f04 f05 f06 ///
	c.f02#c.x c.f03#c.x c.f04#c.x c.f05#c.x c.f06#c.x /// 
	d x c.d#c.x, noomitted vce(cluster id)
  est sto m1
}  
qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 1 f05 = 0 f06 = 0) ///
	subpop(if d == 1) noestimcheck vce(uncond)  post
ereturn display  
est restore m1
qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 0 f05 = 1 f06 = 0) ///
	subpop(if d == 1) noestimcheck vce(uncond) post
ereturn display  
est restore m1
qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 0 f05 = 0 f06 = 1) ///
	subpop(if d == 1) noestimcheck vce(uncond) post
ereturn display	
```
Using `jwdid`:

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
clear all
use nonlinear_did/did_common_6_binary, clear
qui: jwdid y x, ivar(id) tvar(year) trtvar(w) method(regress)
estat event, vce(unconditional)
```

**Using Logit** 

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
clear all
use nonlinear_did/did_common_6_binary, clear
qui {
logit y i.w#c.d#c.f04 i.w#c.d#c.f05 i.w#c.d#c.f06 ///
	i.w#c.d#c.f04#c.x i.w#c.d#c.f05#c.x i.w#c.d#c.f06#c.x ///
	f02 f03 f04 f05 f06 c.f02#c.x c.f03#c.x c.f04#c.x c.f05#c.x c.f06#c.x ///
	d x c.d#c.x, noomitted vce(cluster id)
  est store m1
}  

qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 1 f05 = 0 f06 = 0) ///
	subpop(if d == 1) noestimcheck vce(uncond)  post
ereturn display  
est restore m1
qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 0 f05 = 1 f06 = 0) ///
	subpop(if d == 1) noestimcheck vce(uncond) post
ereturn display  
est restore m1
qui:margins, dydx(w) at(f02 = 0 f03 = 0 f04 = 0 f05 = 0 f06 = 1) ///
	subpop(if d == 1) noestimcheck vce(uncond) post
ereturn display	
```

Using `jwdid`:

```{stata}
*| echo: true
*| code-line-numbers: true
*| code-fold: false
*| classes: larger
clear all
use nonlinear_did/did_common_6_binary, clear
qui: jwdid y x, ivar(id) tvar(year) trtvar(w) method(logit)
estat event, vce(unconditional)
```


## Conclusion

- DID is a popular method for analyzing policy interventions.
- Thanks to the contributions of Wooldridge, Roth and Sant'Anna among others, we have a better understanding of how to implement DID in more complex scenarios.
- One of this important extensions is the use of nonlinear models to better fit the data, and better estimate treatment effects.
- The `jwdid` command is a simple tool that can help you implement the Wooldridge approach to nonlinear DID.


# Thank you

If interested in other work of mine,

Visit my [website](https://friosavila.github.io/)

Follow me on [twitter](https://twitter.com/friosavila)

or use my [Stata packages](https://github.com/friosavila/stpackages)

## References